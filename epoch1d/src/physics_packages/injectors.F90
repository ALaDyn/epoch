! Copyright (C) 2010-2015 Keith Bennett <K.Bennett@warwick.ac.uk>
! Copyright (C) 2009-2017 Chris Brady <C.S.Brady@warwick.ac.uk>
!
! This program is free software: you can redistribute it and/or modify
! it under the terms of the GNU General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version.
!
! This program is distributed in the hope that it will be useful,
! but WITHOUT ANY WARRANTY; without even the implied warranty of
! MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
! GNU General Public License for more details.
!
! You should have received a copy of the GNU General Public License
! along with this program.  If not, see <http://www.gnu.org/licenses/>.

MODULE injectors

  USE partlist
  USE particle_temperature
  USE evaluator
  USE random_generator

  IMPLICIT NONE

CONTAINS

  SUBROUTINE init_injector(boundary, injector)

    INTEGER, INTENT(IN) :: boundary
    TYPE(injector_block), INTENT(INOUT) :: injector

    injector%npart_per_cell = 0
    injector%species = -1
    injector%boundary = boundary
    injector%t_start = 0.0_num
    injector%t_end = t_end
    injector%has_t_end = .FALSE.
    injector%density_min = 0.0_num
    injector%use_flux_injector = .FALSE.
    injector%replenish = .FALSE.

    injector%depth = 1.0_num
    injector%dt_inject = -1.0_num
    NULLIFY(injector%next)

  END SUBROUTINE init_injector



  SUBROUTINE attach_injector(injector)

    TYPE(injector_block), POINTER :: injector
    INTEGER :: boundary

    boundary = injector%boundary

    IF (boundary == c_bd_x_min) THEN
      CALL attach_injector_to_list(injector_x_min, injector)
    ELSE IF (boundary == c_bd_x_max) THEN
      CALL attach_injector_to_list(injector_x_max, injector)
    END IF

  END SUBROUTINE attach_injector



  ! Actually does the attaching of the injector to the correct list
  SUBROUTINE attach_injector_to_list(list, injector)

    TYPE(injector_block), POINTER :: list
    TYPE(injector_block), POINTER :: injector
    TYPE(injector_block), POINTER :: current

    IF (ASSOCIATED(list)) THEN
      current => list
      DO WHILE(ASSOCIATED(current%next))
        current => current%next
      END DO
      current%next => injector
    ELSE
      list => injector
    END IF

  END SUBROUTINE attach_injector_to_list



  SUBROUTINE deallocate_injectors

    CALL deallocate_injector_list(injector_x_min)
    CALL deallocate_injector_list(injector_x_max)

  END SUBROUTINE deallocate_injectors



  SUBROUTINE deallocate_injector_list(list)

    TYPE(injector_block), POINTER :: list
    TYPE(injector_block), POINTER :: current, next
    INTEGER :: i

    current => list
    DO WHILE(ASSOCIATED(current))
      next => current%next
      IF (current%density_function%init) &
          CALL deallocate_stack(current%density_function)
      DO i = 1, 3
        IF (current%temperature_function(i)%init) &
            CALL deallocate_stack(current%temperature_function(i))
        IF (current%drift_function(i)%init) &
            CALL deallocate_stack(current%drift_function(i))
      END DO
      DEALLOCATE(current)
      current => next
    END DO

  END SUBROUTINE deallocate_injector_list



  SUBROUTINE run_injectors

    TYPE(injector_block), POINTER :: current

    IF (x_min_boundary) THEN
      current => injector_x_min
      DO WHILE(ASSOCIATED(current))
        CALL run_single_injector(current, c_bd_x_min)
        current => current%next
      END DO
    END IF

    IF (x_max_boundary) THEN
      current => injector_x_max
      DO WHILE(ASSOCIATED(current))
        CALL run_single_injector(current, c_bd_x_max)
        current => current%next
      END DO
    END IF

  END SUBROUTINE run_injectors



  SUBROUTINE run_single_injector(injector, direction)

    TYPE(injector_block), POINTER :: injector
    INTEGER, INTENT(IN) :: direction
    REAL(num) :: bdy_pos, bdy_space
    TYPE(particle), POINTER :: new
    TYPE(particle_list) :: plist
    REAL(num) :: mass, typical_mc2, p_therm, p_inject_drift, density_grid
    REAL(num) :: gamma_mass, v_inject, density, vol
    REAL(num) :: npart_ideal, itemp, v_inject_s
    REAL(num), DIMENSION(3) :: temperature, drift
    INTEGER :: parts_this_time, ipart, idir, dir_index
    TYPE(parameter_pack) :: parameters
    REAL(num), DIMENSION(3) :: dir_mult
    LOGICAL :: first_inject, flux_fn
#ifdef BOOSTED_FRAME
    REAL(num), DIMENSION(3) :: drift_t
#endif

    IF (time < injector%t_start .OR. time > injector%t_end) RETURN

    ! If you have a moving window that has started moving then unless you
    ! EXPLICITLY give a t_end value to the injector stop the injector
    IF (move_window .AND. window_started .AND. .NOT. injector%has_t_end) &
        RETURN

    flux_fn = .FALSE.
    dir_mult = 1.0_num

    IF (direction == c_bd_x_min) THEN
      parameters%pack_ix = 0
      dir_index = 1
      bdy_pos = x_min
      bdy_space = -dx
      IF (injector%use_flux_injector) THEN
        flux_fn = .TRUE.
        dir_mult(dir_index) = 1.0_num
      END IF
    ELSE IF (direction == c_bd_x_max) THEN
      parameters%pack_ix = nx
      dir_index = 1
      bdy_pos = x_max
      bdy_space = dx
      IF (injector%use_flux_injector) THEN
        flux_fn = .TRUE.
        dir_mult(dir_index) = -1.0_num
      END IF
    ELSE
      RETURN
    END IF

    vol = ABS(bdy_space)

    mass = species_list(injector%species)%mass
    typical_mc2 = (mass * c)**2

    parameters%use_grid_position = .TRUE.

    IF (injector%dt_inject > 0.0_num) THEN
      npart_ideal = dt / injector%dt_inject
      itemp = random_box_muller(0.5_num * SQRT(npart_ideal &
          * (1.0_num - npart_ideal / REAL(injector%npart_per_cell, num)))) &
          + npart_ideal
      injector%depth = injector%depth - itemp
      first_inject = .FALSE.

      IF (injector%depth >= 0.0_num) RETURN
    ELSE
      first_inject = .TRUE.
    END IF

    CALL populate_injector_properties(injector, parameters, density_grid, &
        temperature, drift)

    IF (density_grid < injector%density_min) RETURN

    ! Assume agressive maximum thermal momentum, all components
    ! like hottest component
    p_therm = SQRT(mass * kb * MAXVAL(temperature))
#ifdef BOOSTED_FRAME
    IF (in_boosted_frame) THEN
      !First get gamma factor including thermal correction
      drift_t = drift
      drift_t(dir_index) = drift_t(dir_index)
      drift_t = transform_momentum(global_boost_info, drift_t, &
            mass = mass)
      p_inject_drift = drift_t(dir_index)
      gamma_mass = SQRT(p_inject_drift**2 + typical_mc2) / c

      !Then get just COM drift
      drift_t = transform_momentum(global_boost_info, drift, &
            mass = mass)
      p_inject_drift = drift_t(dir_index)
    ELSE
#endif
      p_inject_drift = drift(dir_index)
      gamma_mass = SQRT((p_therm + p_inject_drift)**2 + typical_mc2) / c
#ifdef BOOSTED_FRAME
    END IF
#endif
      
    v_inject_s = p_inject_drift / gamma_mass
    v_inject = ABS(v_inject_s)

#ifdef BOOSTED_FRAME
    IF (in_boosted_frame .AND. dir_index == 1) parameters%v_prop = v_inject_s
#endif

    injector%dt_inject = ABS(bdy_space) &
        / MAX(injector%npart_per_cell * v_inject, c_tiny)
    IF (first_inject) THEN
      ! On the first run of the injectors it isn't possible to decrement
      ! the optical depth until this point
      npart_ideal = dt / injector%dt_inject
      itemp = random_box_muller(0.5_num * SQRT(npart_ideal &
          * (1.0_num - npart_ideal / REAL(injector%npart_per_cell, num)))) &
          + npart_ideal
      injector%depth = injector%depth - itemp
    END IF

    parts_this_time = FLOOR(ABS(injector%depth - 1.0_num))
    injector%depth = injector%depth + REAL(parts_this_time, num)

    CALL create_empty_partlist(plist)

    DO ipart = 1, parts_this_time
      CALL create_particle(new)

      new%part_pos = bdy_pos + 0.5_num * bdy_space * png &
          - random() * v_inject_s * dt
      parameters%pack_pos = new%part_pos
      parameters%use_grid_position = .FALSE.

      CALL populate_injector_properties(injector, parameters, density, &
          temperature, drift)

      DO idir = 1, 3
        IF (flux_fn) THEN
          new%part_p(idir) = flux_momentum_from_temperature(mass, &
              temperature(idir), drift(idir)) * dir_mult(idir)
        ELSE
          new%part_p(idir) = momentum_from_temperature(mass, &
              temperature(idir), drift(idir))
        END IF
      END DO
#ifdef PER_PARTICLE_CHARGE_MASS
      new%charge = species_list(injector%species)%charge
      new%mass = mass
#endif
#ifndef PER_SPECIES_WEIGHT
      new%weight = vol * density / REAL(injector%npart_per_cell, num)
#endif

#ifdef BOOSTED_FRAME
      IF (in_boosted_frame) THEN
        !No transform of position because it is always through the boundary
        !of the simulation domain
        new%part_p = transform_momentum(global_boost_info, new%part_p, &
            mass = mass)
      END IF
#endif
      CALL add_particle_to_partlist(plist, new)
    END DO

    CALL append_partlist(species_list(injector%species)%attached_list, plist)

  END SUBROUTINE run_single_injector



  SUBROUTINE populate_injector_properties(injector, parameters, density, &
      temperature, drift)

    TYPE(injector_block), POINTER :: injector
    TYPE(parameter_pack), INTENT(IN) :: parameters
    REAL(num), INTENT(OUT) :: density
    REAL(num), DIMENSION(3), INTENT(OUT) :: temperature, drift
    INTEGER :: errcode, i

    errcode = 0
    density = MAX(evaluate_with_parameters(injector%density_function, &
        parameters, errcode), 0.0_num)

#ifdef BOOSTED_FRAME
    IF (in_boosted_frame) density = transform_density(global_boost_info, &
        density)
#endif

    ! Stack can only be time varying if valid. Change if this isn't true
    ! No Lorentz transform of temperature or drift since this is done per
    ! particle
    DO i = 1, 3
      IF (injector%temperature_function(i)%init) THEN
        temperature(i) = &
            MAX(evaluate_with_parameters(injector%temperature_function(i), &
                parameters, errcode), 0.0_num)
      ELSE
        temperature(i) = 0.0_num
      END IF
      IF (injector%drift_function(i)%init) THEN
        drift(i) = &
            evaluate_with_parameters(injector%drift_function(i), &
                                     parameters, errcode)
      ELSE
        drift(i) = 0.0_num
      END IF
    END DO

    IF (errcode /= c_err_none) CALL abort_code(errcode)

  END SUBROUTINE populate_injector_properties

END MODULE injectors
